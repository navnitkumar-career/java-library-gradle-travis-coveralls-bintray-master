apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'com.example'
archivesBaseName = 'foo'
version = '1.0'

if (System.env.TRAVIS_BUILD_NUMBER) {
    version = "${version}.${System.env.TRAVIS_BUILD_NUMBER}"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier 'javadoc'
}

task testsJar(type: Jar) {
    from sourceSets.test.allSource
    classifier 'tests'
}

publishing {
    publications {
        foo(MavenPublication) {
            from components.java
            artifactId archivesBaseName

            pom {
                withXml {
                    def root = asNode()
                    def scm = root.appendNode('scm')
                    scm.appendNode('url', 'https://github.com/pivotal-chicago/java-library-gradle-travis-coveralls-bintray')

                    def issueManagement = root.appendNode('issueManagement')
                    issueManagement.appendNode('system', 'GitHub Issues')
                    issueManagement.appendNode('url', 'https://github.com/pivotal-chicago/java-library-gradle-travis-coveralls-bintray/issues')

                    def licenses = root.appendNode('licenses')
                    def license = licenses.appendNode('license')
                    license.appendNode('name', 'Apache License, Version 2.0')
                    license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                }
            }

            artifact sourcesJar

            artifact javadocJar

            artifact testsJar
        }
    }
}

ext {
    bintrayUser = System.env.BINTRAY_USER ?: null
    bintrayKey = System.env.BINTRAY_KEY ?: null
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = [archivesBaseName]
    publish = true

    pkg {
        repo = 'maven'
        name = archivesBaseName
        desc = 'Example package description.'
        websiteUrl = 'https://github.com/pivotal-chicago'
        issueTrackerUrl = 'https://github.com/pivotal-chicago/java-library-gradle-travis-coveralls-bintray/issues'
        vcsUrl = 'https://github.com/pivotal-chicago/java-library-gradle-travis-coveralls-bintray'
        licenses = ['Apache-2.0']

        version {
            desc = 'Example version description.'
        }
    }
}
